//
// This file is to be applied to every subproject.
//

ext {
    springBootVersion = '1.3.5.RELEASE'
    springCloudVersion = '1.1.0.M4'
}

apply plugin: 'java'
apply plugin: 'maven'

String mavenGroupId = 'org.github.karolykelemen'
String mavenVersion = '1.0'

sourceCompatibility = '1.8'
def compileTasks = [compileJava, compileTestJava]
compileTasks*.options*.encoding = 'UTF-8'
compileTasks*.options*.compilerArgs = ['-Xlint']

repositories {
    mavenCentral();
    maven { url 'http://repo.spring.io/snapshot' }
    maven { url 'http://nexus.ecwid.com/content/groups/public' }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
